apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

configurations {
    ktlint
}

android {
    compileSdkVersion rootProject.maxSdkVersion

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion rootProject.maxSdkVersion
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

ext {
    ktlint_version = '0.36.0'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    api "com.google.android.material:material:$rootProject.material_version"
    implementation "androidx.legacy:legacy-support-v4:$rootProject.legacy_support_version"
    implementation "androidx.appcompat:appcompat:$rootProject.appcompat_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$rootProject.kotlin_version"
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.constraint_layout_version"
    implementation "com.facebook.stetho:stetho:$rootProject.stetho_version"
    implementation "com.facebook.stetho:stetho-okhttp3:$rootProject.stetho_version"

    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$rootProject.lifecycle_version"
    // We are using Kotlin, and not Java 8, so do not use the recommended Java 8 library
    //noinspection LifecycleAnnotationProcessorWithJava8
    kapt "androidx.lifecycle:lifecycle-compiler:$rootProject.lifecycle_version"
    testImplementation "androidx.arch.core:core-testing:$rootProject.core_testing_version"

    // Databinding
    kapt "com.android.databinding:compiler:$rootProject.databinding_version"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$rootProject.kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$rootProject.kotlin_coroutines_version"

    // OK HTTP
    implementation "com.squareup.okhttp3:okhttp:$rootProject.okhttp_version"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofit_version"

    // Stripe Terminal library
    implementation "com.stripe:stripeterminal:$rootProject.stripeTerminalVersion"

    // Leak canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$rootProject.leakcanary_version"

    // Ktlint
    ktlint "com.pinterest:ktlint:$ktlint_version"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}
check.dependsOn ktlint
